plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
    id "com.google.android.libraries.mapsplatform.secrets-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader -> localProperties.load(reader) }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode', '1')
def flutterVersionName = localProperties.getProperty('flutter.versionName', '1.0')

// Force compatible versions for all Google Play Services
configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'com.google.android.gms') {
            if (details.requested.name.startsWith('play-services-measurement')) {
                details.useVersion '21.5.0'
            }
        }
        if (details.requested.group == 'com.google.firebase') {
            if (details.requested.name == 'firebase-analytics') {
                details.useVersion '21.2.0'
            }
            if (details.requested.name == 'firebase-core') {
                details.useVersion '21.1.1'
            }
        }
    }
}
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
android {
    namespace "com.qareeb.rider"
    compileSdkVersion 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.qareeb.rider"
        minSdkVersion 23
        targetSdkVersion 35
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
    }

     signingConfigs {
    release {
        println ">>> Keystore path: ${keystoreProperties['storeFile']}"
        keyAlias keystoreProperties['keyAlias']
        keyPassword keystoreProperties['keyPassword']
        storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
        storePassword keystoreProperties['storePassword']
    }
}



    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        pickFirst '**/libc++_shared.so'
        pickFirst '**/libjsc.so'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/*.kotlin_module'
    }
}

flutter {
    source '../..'
}

secrets {
    propertiesFileName = "secrets.properties"
    ignoreList.add("sdk.*")
}

dependencies {
    implementation "androidx.core:core-ktx:1.12.0"
    implementation 'androidx.multidex:multidex:2.0.1'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

    // Firebase - Using compatible versions
    implementation 'com.google.firebase:firebase-core:21.1.1'
    implementation('com.google.firebase:firebase-analytics:21.2.0') {
        exclude group: 'com.google.android.gms', module: 'play-services-measurement-api'
        exclude group: 'com.google.android.gms', module: 'play-services-measurement-impl'
    }

    // Force compatible Google Play Services versions
    implementation 'com.google.android.gms:play-services-measurement-api:21.5.0'
    implementation 'com.google.android.gms:play-services-measurement-impl:21.5.0'

    // Google Maps (if needed)
    implementation 'com.google.android.gms:play-services-maps:18.2.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
}